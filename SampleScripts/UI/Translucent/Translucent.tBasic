  '------------------------------------------------------------------
  ' Pre-processor prerequisites
  '------------------------------------------------------------------
  #minversion 1.7.6.0
  
  '------------------------------------------------------------------
  ' Declare needed thinBasic modules
  '------------------------------------------------------------------
  USES "UI"

  msgbox 0, "Attention, this example will not work properly on Win9x systems" & $crlf & _
            "including WinMe due to missing functionalities."
  
  '------------------------------------------------------------------
  ' Global declarations
  '------------------------------------------------------------------
    %MAX_Translucent    = 255
    %ID_Track           = 200

    global TranslucenceLevel as long = %MAX_Translucent

  '------------------------------------------------------------------
  ' Main script entry point
  '------------------------------------------------------------------
  function Main() as long
    DIM hDlg    AS LONG

    '---Create a new window
    DIALOG NEW 0, "Translucent Window. Move the trackbar to change translucent level", _
                                        -1, -1, 395, 250, _
                                        %WS_CLIPCHILDREN      or _
                                        %WS_CLIPSIBLINGS      or _
                                        %WS_DLGFRAME          OR _
                                        %DS_CENTER            OR _
                                        %WS_CAPTION           OR _
                                        %WS_SYSMENU           OR _
                                        %WS_OVERLAPPEDWINDOW  ,  _
                                        0 _
                                        TO hDlg

    '---Show new window and assign it callback function
    DIALOG SHOW modal hDlg, call cbDialog_Proc

  end function

  '------------------------------------------------------------------
  ' Window callback handle. Use to handle message pump of main window
  '------------------------------------------------------------------
  callback function cbDialog_Proc() as long

    SELECT CASE cbMsg
      
      CASE %WM_INITDIALOG
        win_SetWindowLong cbhndl, %GWL_EXSTYLE, win_GetWindowLong(cbhndl, %GWL_EXSTYLE) | %WS_EX_LAYERED
        win_SetLayeredWindowAttributes cbhndl, 0, TranslucenceLevel, %LWA_ALPHA

        Control Add trackbar, cbhndl, %ID_Track, "", 5, 20, 380, 30, %WS_CHILD | %TBS_horz | %WS_VISIBLE | %TBS_TOP  | %WS_TABSTOP | %TBS_TOOLTIPS | %TBS_ENABLESELRANGE


        TrackBar_SetRange cbhndl, %ID_Track, 50, %MAX_Translucent
        TrackBar_SetPos   cbhndl, %ID_Track, TranslucenceLevel
        
        control set resize cbhndl, %ID_Track, 1, 1, 0, 0
      
      case %WM_HSCROLL
        '---Check if message was generated by the needed trackbar
        if cblParam = Dialog_GetControl(cbhndl, %ID_Track) then
          TranslucenceLevel = TrackBar_GetPos(cbhndl, %ID_Track)
          win_SetLayeredWindowAttributes cbhndl, 0, TranslucenceLevel, %LWA_ALPHA
        end if

      case %WM_DESTROY

    END SELECT

  end function
  