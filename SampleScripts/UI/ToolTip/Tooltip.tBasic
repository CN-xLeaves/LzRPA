  Uses "UI", "FILE"
              
  Begin ControlID
    %ID_TextBox1  
    %ID_TextBox2
    %ID_TextBox3
    %ID_TextBox4
    %ID_TextBox5
    %ID_Button1
    %ID_StatusBar
    %ID_TIMER
  End ControlID
  
  function Main() as long  
    DIM hDlg    AS LONG
    DIM sFile   AS string
  
  
    DIALOG NEW 0, "TextBox example", -1, -1, 195, 230, _
                                                  %WS_DLGFRAME OR %DS_CENTER OR %WS_CAPTION OR %WS_SYSMENU, _
                                                  0 TO hDlg

    CONTROL ADD TEXTBOX , hDlg, %ID_TextBox1, "Text box: left allign"  , 5,  15, 185, 15, %WS_BORDER OR %WS_TABSTOP                  , %WS_EX_CLIENTEDGE
    control set tooltip   hdlg, %ID_TextBox1, "The text inside this box will be left alligned", "This is the title", 1
    
    CONTROL ADD TEXTBOX , hDlg, %ID_TextBox2, "Text box: center allign", 5,  30, 185, 15, %WS_BORDER OR %WS_TABSTOP OR %ES_CENTER    , %WS_EX_CLIENTEDGE
    control set tooltip   hdlg, %ID_TextBox2, "The text inside this box will be alligned to the center"

    CONTROL ADD TEXTBOX , hDlg, %ID_TextBox3, "Text box: right allign" , 5,  45, 185, 15, %WS_BORDER OR %WS_TABSTOP OR %ES_RIGHT     , %WS_EX_CLIENTEDGE
    control set tooltip   hdlg, %ID_TextBox3, "The text inside this box will be right alligned"

    CONTROL ADD TEXTBOX , hDlg, %ID_TextBox4, "Text box: read only"    , 5,  60, 185, 15, %WS_BORDER OR %WS_TABSTOP OR %ES_READONLY  , %WS_EX_CLIENTEDGE
    control set tooltip   hdlg, %ID_TextBox4, "Read only text box." & $crlf & "You will not be able to edit text inside this box."

    CONTROL ADD TEXTBOX , hDlg, %ID_TextBox5, "" , 5,  75, 185, 100, %WS_BORDER      OR _
                                                           %WS_TABSTOP     OR _
                                                           %ES_WANTRETURN  OR _
                                                           %ES_MULTILINE   OR _
                                                           %ES_AUTOHSCROLL OR _
                                                           %ES_AUTOVSCROLL OR _
                                                           %WS_VSCROLL     OR _
                                                           %WS_HSCROLL, _
                                                           %WS_EX_CLIENTEDGE

    CONTROL ADD BUTTON , hDlg, %ID_Button1, "Open a file"  , 5,  200, 185, 15


    '---This will load current script into control number 50
    sFile = FILE_LOAD(App_SourceName)
    CONTROL SET TEXT hDlg, %ID_TextBox5, sFile
    
    '---Show dialog in MODAL state
    DIALOG SHOW MODAL hDlg, call dlgCallback
  end function  
  
  callback function dlgCallback() as long
    local sFile as string
    
    '---Now test the message
    SELECT CASE cbMsg

      CASE %WM_COMMAND
        SELECT CASE cbCtl
          CASE %ID_Button1
            if cbCtlMsg = %BN_CLICKED then
              sFile = OpenFile(cbHndl)
              if sFile <> "" then
                CONTROL SET TEXT cbHndl, %ID_TextBox5, FILE_LOAD(sFile)
                msgbox cbHndl, "File " & sFile & " loaded into textbox"
              else
                msgbox cbHndl, "No file selected"
              end if
            end if
        END SELECT

    END SELECT
  end function
  
  function OpenFile(hDlg as long) as string
    dim sFile   as string
    dim sFilter as string

    sFilter  = "thinBasic Files (*.tBasic, *.tBasicc)|*.tBasic;*.tBasicc|"
    sFilter += "Basic Files (*.BAS, *.INC)|*.BAS;*.INC|"
    sFilter += "Resource Files (*.RC)|*.RC|"
    sFilter += "Help files (*.HLP)|*.HLP|"
    sFilter += "Text Files (*.TXT)|*.TXT|"
    sFilter += "Word processing (*.RTF)|*.RTF|"
    sFilter += "All Files (*.*)|*.*"

    sFile = Dialog_OpenFile(hDlg, _
                            "Open an file", _
                            DIR_GetCurrent, _
                            sFilter, _
                            "tBasic", _
                            %OFN_FILEMUSTEXIST OR %OFN_HIDEREADONLY or %OFN_ENABLESIZING)
    function = sFile
  end function
  

