USES "FILE"
Uses "BIFF" 
uses "Console"

FUNCTION TotalFileSize(lDir AS STRING) AS QUAD
  LOCAL Count     AS LONG
  Local FileName  As String                   
  LOCAL nItems    AS LONG
  LOCAL TotSize   AS QUAD
  Local fSize     As Quad                   
  Local Files()   As String

  nItems = DIR_ListArray(Files, lDir, "*.DLL", %FILE_NORMAL Or %FILE_HIDDEN Or %FILE_SYSTEM Or %FILE_ADDPATH)
  
  IF nItems > 0 THEN
    FOR Count = 1 TO nItems     
      INCR vRow

      FileName = Files(Count)
      
      fSize = FILE_SIZE(FileName)
      TotSize = TotSize + fSize
      BIFF_WriteNumber  (fSize, vRow, vCol + 2)
      BIFF_WriteText    (FileName, vRow, vCol + 1)
      BIFF_WriteText    (FILE_GetVersionString(FileName), vRow, vCol + 3)
      console_writeLine format$(vRow, "00000") & " " & FileName & " Version: " & FILE_GetVersionString(FileName)

    NEXT
  END IF                  
  FUNCTION = TotSize
  
END FUNCTION
 
FUNCTION ListDir(lDir AS STRING) AS LONG

  LOCAL Count   AS LONG
  Local Item    As String                   
  LOCAL nItems  AS LONG 
  Local CurrentRow  As Long               
  Local Directories() As String
  
  LOCAL TFS AS QUAD
  
  nItems = DIR_ListArray(Directories, lDir, "*.*", %FILE_SUBDIR Or %FILE_HIDDEN Or %FILE_ADDPATH)
  
  IF nItems > 0 THEN
    FOR Count = 1 TO nItems
      Item = Directories(Count)
      CurrentRow = vRow
      BIFF_WriteText    (Item, CurrentRow, vCol)
      TFS = TotalFileSize(Item)
      BIFF_WriteNumber  (TFS, CurrentRow, vCol - 1)
      INCR vRow
      ListDir(Item)
    NEXT
  END IF
  
END FUNCTION

'-------------------------------------------------------------------------
'Start Main Program
'-------------------------------------------------------------------------
DIM vCol        AS LONG VALUE 2
DIM vRow        AS LONG VALUE 2
DIM OutFileName AS STRING 
DIM StartingDir AS STRING
DIM T1, T2      AS DOUBLE
DIM TFS         AS QUAD

T1 = TIMER
  
OutFileName = App_sourcepath + "ThinBasic_DLL_Version.xls"
StartingDir = APP_PATH

File_Kill(OutFileName)
BIFF_SetBuffer(1024 * 1024)
BIFF_CreateFile(OutFileName)

BIFF_WriteText    ("DirSize(Bytes)", vRow - 1, vCol - 1)
BIFF_WriteText    ("DirName", vRow - 1, vCol)

BIFF_WriteText    (StartingDir, vRow, vCol)

TFS = TotalFileSize(StartingDir)
BIFF_WriteNumber  (TFS, vRow, vCol - 1)

INCR vRow

ListDir(StartingDir)

INCR vRow

T2 = TIMER
BIFF_WriteText ("TOTAL TIME (Secs): " & FORMAT$(T2 - T1, "#0.0000"), vRow, vCol)

BIFF_CloseFile  

PrintL
PrintL "Check " + OutFileName + " for results..."
PrintL "Press any key to continue..."
WaitKey

